PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    ?forumId
    ?forumTitle
    ?moderatorId
    ?moderatorFirstName
    ?moderatorLastName
WHERE {
    {
        SELECT
            ?message
            ?messageId
        WHERE
        {
            BIND( $personId AS ?rootId )
            ?rootPerson a snvoc:Person .
            ?rootPerson snvoc:id ?rootId .
            ?message snvoc:hasCreator ?rootPerson .
            ?message snvoc:id ?messageId .
        }
        LIMIT 1
    } .
    OPTIONAL {
        #the variadic path was transformed to rule
        ?message snvoc:replyOf ?originalPostInner .
        ?originalPostInner a snvoc:Post .
    } .
    BIND( COALESCE(?originalPostInner, ?message) AS ?originalPost ) .
    ?forum snvoc:containerOf ?originalPost .
    ?forum snvoc:id ?forumId .
    ?forum snvoc:title ?forumTitle .
    ?forum snvoc:hasModerator ?moderator .
    ?moderator snvoc:id ?moderatorId .
    ?moderator snvoc:firstName ?moderatorFirstName .
    ?moderator snvoc:lastName ?moderatorLastName .
}

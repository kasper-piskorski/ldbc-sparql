PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    ?personId
    ?firstName
    ?lastName
    ?friendshipCreationDate
WHERE
{
    BIND( $personId AS ?rootId )
    ?rootPerson a snvoc:Person .
    ?rootPerson snvoc:id ?rootId .
    {
        SELECT
            ?person
            ?knows
        WHERE
        {
            {
                ?rootPerson snvoc:knows ?intermediateKnows .
                ?intermediateKnows snvoc:hasPerson ?intermediatePerson .
                ?intermediatePerson snvoc:knows ?intermediateKnows2 .
                ?intermediateKnows2 snvoc:hasPerson ?intermediatePerson2 .
                ?intermediatePerson2 snvoc:knows ?knows .
                ?knows snvoc:hasPerson ?person .
            } UNION {
                ?person snvoc:knows ?knows .
                ?knows snvoc:hasPerson ?intermediatePerson2 .
                ?intermediatePerson2 snvoc:knows ?intermediateKnows2 .
                ?intermediateKnows2 snvoc:hasPerson ?intermediatePerson .
                ?intermediatePerson snvoc:knows ?intermediateKnows .
                ?intermediateKnows snvoc:hasPerson ?rootPerson .
            }
        }
        LIMIT 200000
    } .
    ?knows snvoc:creationDate ?friendshipCreationDate .
    ?person snvoc:firstName ?firstName .
    ?person snvoc:lastName ?lastName .
    ?person snvoc:id ?personId .
}
ORDER BY DESC(?friendshipCreationDate) ?personId
